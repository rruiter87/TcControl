<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="Pid" Id="{da73802b-2471-4be1-ae33-77c4e2eb36c6}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Pid IMPLEMENTS IPid, IOut
VAR_INPUT
    setpoint : LREAL;
    actual : LREAL;
    parameters : PidParameters;
    cycleTime : Millisecond := 1;
END_VAR
VAR
    _error : LREAL;
    _previousError : LREAL;
    _deltaError : LREAL;
    _proportionalPart : LREAL;
    _integralPart : LREAL;
    _previousIntegralPart : LREAL;
    _differentialPart : LREAL;
    _differentialPartLimit : LREAL;
    _out : LREAL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[_error := setpoint - actual;

_proportionalPart := parameters.Kp * _error;

_integralPart := _previousIntegralPart + parameters.Ki * _error * cycleTime;
_previousIntegralPart := _integralPart;

IF cycleTime <> 0 THEN
    _deltaError := (_error - _previousError) / cycleTime;
ELSE
    ADSLOGSTR(msgCtrlMask:=ADSLOG_MSGTYPE_ERROR, msgFmtStr:='Enter a cycletime > 0!', strArg:='');
END_IF
IF _differentialPartLimit = 0 OR _deltaError <= _differentialPartLimit THEN
    _differentialPart := _deltaError * parameters.Kd;
END_IF
_previousError := _error;

_out := _proportionalPart + _integralPart + _differentialPart;
]]></ST>
    </Implementation>
    <Property Name="DifferentialPartLimit" Id="{0e5fc49d-15ed-446b-b5c4-b8f8bc7054d6}">
      <Declaration><![CDATA[(* Limit the differential part. Convenient when the output is calculated for the first time.
The limit prevents it from having a very large value after the first call.*)
PROPERTY DifferentialPartLimit : LREAL
]]></Declaration>
      <Set Name="Set" Id="{ebaea50d-eb7b-45f8-9ebf-1ac59fe2b99d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_differentialPartLimit := DifferentialPartLimit;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="Out" Id="{15ef5403-f0db-4dd2-9900-07e6b4494afc}">
      <Declaration><![CDATA[PROPERTY Out : LREAL
]]></Declaration>
      <Get Name="Get" Id="{72cbacfe-7ca3-43fc-b241-9d203cf10514}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Out := _out;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="PreviousIntegralPart" Id="{84c7dc6b-7c17-4d37-ad09-62b2cacd0ffa}">
      <Declaration><![CDATA[(* Set previous integral part on first call. Prevents integrator term for needing to build up. *)
PROPERTY PreviousIntegralPart : LREAL
]]></Declaration>
      <Set Name="Set" Id="{f9f66342-ded5-4cda-8f18-8e494b23a987}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_previousIntegralPart := PreviousIntegralPart;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>